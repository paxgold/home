# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtk
pkgname=(gtk)
pkgver=3.24.31
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(cairo libepoxy gdk-pixbuf2
         dconf pango shared-mime-info
         wayland libxkbcommon json-glib librsvg
         desktop-file-utils mesa iso-codes)
makedepends=(git sassc meson
             wayland-protocols)
license=(LGPL)
_commit=ab45bde94c7bbd140b12fa0dd6203f7b98d1a715  # tags/3.24.31^0
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
gtk-query-immodules-3.0.hook)
sha256sums=('SKIP'
            'de46e5514ff39a7a65e01e485e874775ab1c0ad20b8e94ada43f4a6af1370845')

pkgver() {
  cd gtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtk
}

build() {
  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  local meson_options=(
    -D broadway_backend=false \
    -D win32_backend=false \
    -D quartz_backend=false \
    -D cloudproviders=false \
    -D profiler=false \
    -D tracker3=false \
    -D colord=no \
    -D gtk_doc=false \
    -D man=false \
    -D introspection=false \
    -D demos=false \
    -D examples=false \
	-D tests=false \
	-D installed_tests=false \
	-D x11_backend=true \
	-D wayland_backend=true \
	-D xinerama=no \
	-D print_backends=auto
  )

  arch-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gtk() {
  optdepends=('evince: Default print preview command')
  provides=(gtk3 libgtk-3.so libgdk-3.so libgailutil-3.so)
  conflicts=(gtk3)
  replaces=()
  install=gtk3.install

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-3.0/settings.ini" <<END
[Settings]
gtk-theme-name=Materia-dark-compact
gtk-icon-theme-name=Flat-Remix-Green
gtk-font-name=Sans 11
gtk-cursor-theme-name=Adwaita
gtk-cursor-theme-size=24
gtk-toolbar-style=GTK_TOOLBAR_BOTH_HORIZ
gtk-toolbar-icon-size=GTK_ICON_SIZE_SMALL_TOOLBAR
gtk-button-images=0
gtk-menu-images=0
gtk-enable-event-sounds=0
gtk-enable-input-feedback-sounds=0
gtk-xft-antialias=1
gtk-xft-hinting=1
gtk-xft-hintstyle=hintsfull
gtk-xft-rgba=rgb
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-query-immodules-3.0.hook

  cd "$pkgdir"

  rm usr/bin/gtk-update-icon-cache
}

# vim:set ts=2 sw=2 et:
